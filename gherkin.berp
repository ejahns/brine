[
	Tokens -> #Empty,#Comment,#TagLine,#FeatureLine,#BackgroundLine,#ScenarioLine,#ScenarioOutlineLine,#ExamplesLine,#StepLine,#DocStringSeparator,#TableRow,#Language
	IgnoredTokens -> #Comment,#Empty
	ClassName -> Parser
	Namespace -> Gherkin
]

Feature! := #Language? #TagLine* #FeatureLine #Other* Background? (Scenario|ScenarioOutline)*

Background! := #BackgroundLine #Other* Step*

Scenario! [#Empty|#Comment|#TagLine->#ScenarioLine]:= #TagLine* #ScenarioLine #Other* Step*

ScenarioOutline! [#Empty|#Comment|#TagLine->#ScenarioOutlineLine]:= #TagLine* #ScenarioOutlineLine #Other* Step* ExamplesDefinition*

// after the first "Examples" block, interpreting a tag line is ambiguous (tagline of next examples or of next scenario)
// because of this, we need a lookahead hint, that connects the tag line to the next examples, if there is an examples block ahead
ExamplesDefinition! [#Empty|#Comment|#TagLine->#ExamplesLine]:= #TagLine* Examples
Examples! := #ExamplesLine #Other* ExamplesTable?
ExamplesTable! := #TableRow #TableRow*

Step! := #StepLine (DataTable | DocString)?

DataTable! := #TableRow+
DocString! := #DocStringSeparator #Other* #DocStringSeparator